/*
 * AndFHEM - Open Source Android application to control a FHEM home automation
 * server.
 *
 * Copyright (c) 2011, Matthias Klass or third-party contributors as
 * indicated by the @author tags or express copyright attribution
 * statements applied by the authors.  All third-party contributions are
 * distributed under license by Red Hat Inc.
 *
 * This copyrighted material is made available to anyone wishing to use, modify,
 * copy, or redistribute it subject to the terms and conditions of the GNU GENERAL PUBLIC LICENSE, as published by the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY
 * or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU GENERAL PUBLIC LICENSE
 * for more details.
 *
 * You should have received a copy of the GNU GENERAL PUBLIC LICENSE
 * along with this distribution; if not, write to:
 *   Free Software Foundation, Inc.
 *   51 Franklin Street, Fifth Floor
 *   Boston, MA  02110-1301  USA
 */


def getVersionCode(version) {
    return Integer.parseInt(
            version.replaceAll("\\.", "")
                    .replaceAll("-SNAPSHOT", "")
                    .replaceAll("-BETA", "")
    )
}

def getNextVersionCode(version) {
    def versionCode = getVersionCode(version)
    return project.getVersion().endsWith("-SNAPSHOT") ? versionCode : versionCode + 1
}

def getNextVersion(version) {
    println("version is $version")
    def nextCode = String.valueOf(getNextVersionCode(version))
    def rev = nextCode.charAt(nextCode.length() - 1)
    def minor = nextCode.charAt(nextCode.length() - 2)
    def major = nextCode.substring(0, nextCode.length() - 2)
    return "$major.$minor.$rev"
}

play {
    userFraction = 1.0
}

release {
    versionPropertyFile = '../gradle.properties'
    versionProperties = ['version']
    failOnSnapshotDependencies = false
    // this is a temporary workround until version 3.0 is released, so that the plugin can be moved to the root project

    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}

task setStage {
    setStage(System.getProperty('stage'))
}
beforeReleaseBuild.dependsOn('setStage')

def setStage(stage) {
    if (stage == 'production') {
        play.track = 'production'
        release.buildTasks = ['generateWhatsNew', 'build', 'uploadToPlayStore', 'githubRelease']
        setNewReleaseVersion(getNextVersion(project.version))
        afterReleaseBuild.dependsOn("resetWhatsNew")
    } else if (stage == 'beta') {
        println("releaseBeta")
        play.track = 'beta'
        release.buildTasks = ['generateWhatsNew', 'build', 'uploadToPlayStore']
        setNewReleaseVersion(getNextVersion(project.version) + "-BETA")
        System.setProperty("release.useAutomaticVersion", "true")
    } else {
        release.buildTasks = []
    }
}

task uploadToPlayStore
uploadToPlayStore.dependsOn {
    tasks.findAll { it.name.startsWith('publish') && it.name.endsWith("Release") }
}

task deleteGitTag(type: Exec) {
    commandLine 'git', 'tag', '-d', getNewReleaseVersion()
    ignoreExitValue true
}
tasks.findAll { it.name.contains("createReleaseTag") }
        .forEach { it.dependsOn deleteGitTag }

def getNewReleaseVersion() {
    return System.getProperty("release.releaseVersion")
}

def setNewReleaseVersion(value) {
    if (getNewReleaseVersion() == null) {
        println("set new version to " + value)
        System.setProperty("release.releaseVersion", value)

        println("set new version after $value")
        System.setProperty("release.newVersion", getNextVersion(value) + "-SNAPSHOT")
    }
}